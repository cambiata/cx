/*
 *  Templo compiler
 *  Copyright (c)2008 Motion-Twin
 *
 *  This library is free software; you can redistribute it and/or
 *  modify it under the terms of the GNU Lesser General Public
 *  License as published by the Free Software Foundation; either
 *  version 2.1 of the License, or (at your option) any later version.
 *
 *  This library is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 *  Lesser General Public License or the LICENSE file for more details.
 */
open Templo.Ast;

function report(msg,p) {
	if p == Lexer.null_pos then	{
		Stack.dump IO.stderr Stack.exc();
		IO.printf IO.stderr "Exception : %s\n" msg
	} else
	IO.printf IO.stderr "%s:%d: %s\n" (Lexer.source p,Lexer.line p,msg);
	Sys.exit(-1);
}

function rec open_file(paths,file) {
	match paths {
	| [] -> error ("File not found " + file)
	| d :: paths -> try IO.read_file (d+file) true catch { _ -> open_file paths file }
	}
}

function parse(file,ch) {
	var lex = Lexer.create Buffer.create();
	Lexer.input lex file ch 1 0;
	var el = Templo.Parser.parse lex;
	IO.close_in ch;
	el
}

function complete_path(p) {
	var len = String.length p;
	if len > 0 && String.get p (len - 1) != '/' && String.get p (len - 1) != '\\' then
		p + "/"
	else
		p
}

var rspaces = Regexp.build "[ \r\n\t]+";

function ltrim(s) {
	var r = Regexp.build "^[ \r\n\t]+(.*)$";
	if Regexp.find r s 0 (String.length s) then Regexp.matched r 1 else s
}

function rtrim(s) {
	var r = Regexp.build "^(.*)[ \r\n\t]+$";
	if Regexp.find r s 0 (String.length s) then Regexp.matched r 1 else s
}

function rec compact(el) {
	function make(s,sp) {
		String.concat (if sp then " " else "\n") (Regexp.split rspaces s)
	}
	function rec loop(pre,el) {
		match el {
		| [] ->
			match pre {
			| None -> []
			| Some (str,p) -> [(XData (make str true),p)]
			}
		| (XData str,p) :: l ->
			match pre {
			| None -> loop (Some (str,p)) l
			| Some (str2,_) -> loop (Some (str2+str,p)) l
			}
		| x :: l ->
			match pre {
			| None -> x :: loop None l
			| Some (str,p) -> (XData (make str false),p) :: x :: loop None l
			}
		}
	}
	function trim(c) {
		var c = match c {
		| (XData str,p) :: l -> (XData ltrim(str),p) :: l
		| _ -> c
		};
		var c = match List.rev c {
		| (XData str,p) :: l -> List.rev ((XData rtrim(str),p) :: l)
		| _ -> c
		}
		c
	}
	function loop2((e,p)) {
		var e = match e {
		| XNode n when n.n_node == "script" ->
			match n.n_content {
			| Some c -> n.n_content := Some (trim c)
			| _ -> ()
			}
			XNode n
		| XNode n when n.n_node != "pre" ->
			n.n_content := match n.n_content { None -> None | Some c -> Some (trim (compact c)) }
			XNode n
		| XMacroDef m ->
			var c = match m.m_content { MContent c -> MContent (trim (compact c)) | c -> c };
			XMacroDef ({ m_mode = m.m_mode; m_name = m.m_name; m_content = c; m_args = m.m_args })
		| _ -> e
		}
		(e,p)
	}
	List.map loop2 (loop None el);
}

try {
	var head = "Temploc v2.3 - (c)2008-2009 Motion-Twin\nUsage : temploc [options] <files>";
	var files = &[];
	var paths = &[""];
	var output = &"";
	var target = &Templo.Neko.generate;
	var compare_xml = &None;
	var debug = &false;
	var xml = &false;
	var do_compact = &false;
	var chk = Templo.Checker.init();
	var macros = &[];
	var compares = &[];
	var decl = [
		("-cp",Args.String (function(p) paths := complete_path p :: *paths),"<dir> : add file search path");
		("-output",Args.String (function(o) output := complete_path o),"<dir> : specify output directory");
		("-macros",Args.String (function(f) macros := f :: *macros),"<file> : add macro definition file");
		("-php",Args.Void (function() target := Templo.Php.generate),": use php output");
		("-debug",Args.Void (function() debug := true),": use debug mode");
		("-xml",Args.Void (function() xml := true),": don't use XHTML checks");
		("-compare",Args.String (function(str) {
			target := function(_,_,_) {}
			match String.split str "@" {
			| [a;b] -> compares := (a,b) :: *compares;
			| _ -> error ("Invalid comparison pattern : " + str);
			}
		}),"<file1@file2> : compare two files or directories structurally");
		("-compare-xml",Args.String (function(cfg) {
			var f = open_file (*paths) cfg;
			compare_xml := Some (IO.read_all f);
			IO.close_in f
		}),"<config> : compare also XML files");
		("--compare-both",Args.Void (function() {
			Templo.Compare.original := true;
		}),": display both files position in comparisons");
		("--compact", Args.Void (function() do_compact := true),": compact html spaces");
	];
	Args.parse head decl (function(file) files := file :: *files);
	// remove all macros that are also in files
	files := List.fold (function(acc,file) List.filter (function(f) f != file) acc) (*files) (*macros);
	function is_xml(file) {
		*xml || String.lowercase (Sys.extension file) == "xml"
	}
	function compile(file,macro) {
		var el = parse file open_file(*paths,file);
		var el = if macro then Templo.Checker.add_macros chk file el else el;
		var el, deps = Templo.Checker.run chk is_xml(file) el;
		var el = if *do_compact then compact el else el;
		(file,el,deps)
	}
	var macros = List.map (function(file) compile(file,true)) List.rev(*macros);
	var files = List.map (function(file) compile(file,false)) List.rev(*files);
	List.iter (function((a,b)) {
		var macros = List.concat (List.map (function((_,el,_)) {
			List.map (function(e)
				match fst e {
				| XMacroDef m -> m
				| _ -> assert()
				}
			) el
		}) macros);
		Templo.Compare.init (*debug) (*compare_xml) macros;
		Templo.Checker.error_ref := Some (function(msg,p) IO.printf IO.stderr "%s:%d: %s\n" (Lexer.source p,Lexer.line p,msg));
		if !(Templo.Compare.compare a b (function(f) { var _,el,_ = compile f false; el }) IO.stdout) then
			IO.printf IO.stdout "No files were compared in '%s' and '%s'" (a,b);
	}) List.rev(*compares);
	var datas = List.append macros files;
	(*target) (*output) (*debug) datas;
} catch {
	| Templo.Lexer.Error(msg,pos) -> report Templo.Lexer.error_msg(msg) pos
	| Templo.Parser.Error(msg,pos) -> report Templo.Parser.error_msg(msg) pos
	| Templo.Checker.Error(msg,pos) -> report msg pos
	| Neko.Compile.Error(msg,pos) -> report msg pos
	| Error msg ->
		IO.printf IO.stderr "%s\n" msg;
		Sys.exit(-1)
	| e ->
		Stack.dump IO.stderr Stack.exc();
		IO.printf IO.stderr "Exception : %s\n" string(e);
		Sys.exit (-1);
}
